import "Operations.eol";

pre {
	var lifelines = UML!Lifeline.all;
	var dynamicViewName = "dynamicView";
	var staticViewComponentsName = "staticView-components";
	var staticViewClassesName = "staticView-classes";
	var deploymentViewName = "deploymentView";
	var validPackageNames = new Sequence();
	validPackageNames.add(staticViewComponentsName);
	validPackageNames.add(staticViewClassesName);
	validPackageNames.add(dynamicViewName);
	validPackageNames.add(deploymentViewName);
	
	// stereotypes
	var gaScenario = "GaScenario";
	var gaExecHost = "GaExecHost";
	var gaWorkloadEvent = "GaWorkloadEvent";
	var gaStep = "GaStep";
	
	
	// other
	var gaWorkloadEventPattern = "(closed|open):[0-9]+(.[0-9]+)?";
}

// model structure
context UML!Model {
	
	constraint ExactlyOneModel {
		check : UML!Model.all.size() == 1
		message : "Exactly one model-element is allowed. The model element count is: " + UML!Model.all.size()
	}
	
}

context UML!Package {
	
	constraint HasValidName {
		guard : not self.isTypeOf(UML!Model)
		check : validPackageNames.contains(self.name)
		message {
			return "The valid Package-Names are: " + validPackageNames.concat(", ") + "\n"
			 		+ " The given name is: " + self.name;
		}
	}
	
}


// Dynamic View
context UML!UseCase {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of UseCase is not defined: " + self
    }

	constraint OwnerIsPackage {
		check : self.getOwner().isDefined() and self.getOwner().isTypeOf(UML!Package)
		message: "Owner of UseCase '" + self.name + "' is not defined or not a package. Owner: " + self.getOwner()
	}
	
	constraint IsInDynamicViewPackage {
		check : self.getOwner().isDefined() and self.getOwner().name == dynamicViewName
		message: "Owner of UseCase '" + self.name + "' is not the " + dynamicViewName + "Package. Owner: " + self.getOwner()
	}
	
	constraint HasInteraction {
		check : self.hasInteraction()
		message : "UseCase '" + self.name + "' has no interaction. Behaviours: " + self.getOwnedBehaviors()
	}
	
	constraint GaScenarioStereotypeApplied {
		check : self.isStereotypeApplied(gaScenario)
		message : self.getMessageStereotypeNotApplied(gaScenario)
	}
}

context UML!Interaction {
	
	constraint InteractionHasLifelineWithActor {
		check : self.getLifelines().stream().filter(lf|lf.getRepresents().type.isTypeOf(UML!Actor)).count() == 1
		message : "Exactly one lifeline was expected to be connected to an actor, found where the following: " + self.getLifelines().stream().filter(lf|lf.getRepresents().isTypeOf(UML!Actor)).collect(Native("java.util.stream.Collectors").toList())
	}

	constraint InteractionHasLifelineEntry {
		check : self.getLifeline("'Entry'").isDefined()
		message : "Lifeline with name 'Entry' was expected, but not found."
	}
	
	constraint HasLifelines {
		check : self.lifelines.isDefined() and self.lifelines.size() > 0
	}
	
	constraint AtLeastOneGaWorkflowStereotypeOnLifeline {
		check : self.lifelines.stream().filter(lf | lf.isStereotypeApplied(gaWorkloadEvent) ).findFirst().isPresent()
		message : "No lifeline was found that applies the Stereotype '" + gaWorkloadEvent + "' on interaction: " + self
	}

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Interaction is not defined: " + self
    }

	
}

context UML!Lifeline {

    constraint OnlyActorLifelineIsGaWorkload {
        guard : self.isStereotypeApplied(gaWorkloadEvent)
        check : self.represents.type.isTypeOf(UML!Actor)
    }

    constraint RepresentsComponent {
        guard : not self.isStereotypeApplied(gaWorkloadEvent)
        check : self.represents.type.isTypeOf(UML!Component)
    }

	constraint WorkloadMatchesPattern {
		guard : self.isStereotypeApplied(gaWorkloadEvent)
		check : self.getStereotypeValue(gaWorkloadEvent, "pattern").matches(gaWorkloadEventPattern)
		message : "The Stereotype '" + gaWorkloadEvent + "' on Lifeline '" + self + "' does not match the pattern '" + gaWorkloadEventPattern + "'. Actual Value: " + self.getStereotypeValue(gaWorkloadEvent, "pattern")
	}

	constraint NameIsSet {
		check : self.name.isDefined()
		message : "Name of lifeline is not defined: " + self
	}
	
	constraint BehaviorExecutionSpecificationIsSet {
	    guard : not self.name.equals("'Entry'")
		check  {
			var covered = self.getCoveredBys();
			for (c : UML!BehaviorExecutionSpecification in covered ) {
				return true;
			}
			return false;
		}
		message : "The lifeline '" + self.name + "' has no BehaviorExecutionSpecification"
	}
	
	constraint EntryHasThreeCoveredBy {
		guard : false
		check : self.getCoveredBys().size() == 3
	}
	
	constraint EntryHasOneBehaviorExecutionSpecification {
		guard : false
		check : self.getCoveredBys().stream().filter(c|c.isTypeOf(UML!BehaviorExecutionSpecification)).count() == 1
	}
	
}

context UML!Message {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Message is not defined: " + self
    }
	
	constraint HasSendEvent {
		check : self.getSendEvent().isDefined()
	}

	constraint HasReceiveEvent {
		check : self.getReceiveEvent().isDefined()
	}
	
	constraint HasMessageSort {
		check : self.getMessageSort().isDefined()
		message : "MessageSort is required for Message: " + self
	}
	
	constraint MessageSortMatchesValue {
		check : self.getMessageSort().getName() == "synchCall" or self.getMessageSort().getName() == "reply"
		message : "Unsupported Type for MessageSort '" + self.getMessageSort().getName() + "' on message: " + self
	}
}

context UML!BehaviorExecutionSpecification {

    constraint GaStepStereotypeApplied {
        check : self.isStereotypeApplied(gaStep)
        message : self.getMessageStereotypeNotApplied(gaStep)
    }

    constraint GaStepAttributeRepetitionApplied {
        check : self.isStereotypeApplied(gaStep) and self.getValue(gaStep, "rep").isDefined()
        message : "The 'rep' attribute is not set for BES: " + self
    }

    constraint GaStepAttributeExecutionTimeApplied {
        check : self.isStereotypeApplied(gaStep) and self.getValue(gaStep, "execTime").isDefined()
        message : "The 'execTime' attribute is not set for BES: " + self
    }

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of BehaviorExecutionSpecification is not defined: " + self
    }

	constraint ExactlyOneCovered {
		check : self.getCovereds().size() == 1
	}
	
	constraint CoveredIsLifeline {
		check : self.getCovereds().size() == 1 and self.getCovereds().get(0).isTypeOf(UML!Lifeline)
	}
	
	constraint StartIsSet {
		check : self.getStart().isDefined()
	}

	constraint FinishIsSet {
		check : self.getFinish().isDefined()
	}
	
}

context UML!MessageOccurrenceSpecification {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of MessageOccurrenceSpecification is not defined: " + self
    }

	constraint MessageIsDefined {
		check : self.getMessage().isDefined()
	}
	
	constraint ExactlyOneCovered {
		check : self.getCovereds().size() == 1
	}
	
	constraint CoveredIsLifeline {
		check : self.getCovereds().get(0).isTypeOf(UML!Lifeline)
	}
}

// Static View

context UML!Component {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Component is not defined: " + self
    }

	constraint ComponentIsManifestedInArtifact {
		check : UML!Artifact.all.stream().flatMap(a|a.getManifestations().stream()).filter(m|m.getSupplier(self.name).isDefined()).findFirst().isPresent()
		message: "Component is not manifested in any artifact: " + self
	}

	constraint ComponentIsRepresentedAsLifeline {
	    guard : not (self.name == "'Entry'")
		check : UML!Lifeline.all.stream().filter(l | l.represents.type == self).findFirst().isPresent()
		message: "Component is not represented in any Lifeline: " + self

	}

	constraint ComponentHasOperation {
	    check : self.ownedOperations.isDefined() and self.ownedOperations.size() > 0
	}
	
}

context UML!Interface {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Interface is not defined: " + self
    }

	constraint IsRealizedInComponent {
		check : UML!Component.all.stream().filter(c | c.getInterfaceRealization(self.name, self).isDefined()).findFirst().isPresent()
	}
	
}

context UML!Usage {
	constraint HasExactlyOneSupplier {
		check : self.getSuppliers().size() == 1
	}

	constraint HasExactlyOneClient {
		check : self.getClients().size() == 1
	}

}


// Deployment View

context UML!Node {
    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Node is not defined: " + self
    }

	constraint GaExecHostStereotypeApplied {
		check : self.isStereotypeApplied(gaExecHost)
		message : self.getMessageStereotypeNotApplied(gaExecHost)
	}
	
	constraint NodeHasDeployedArtifacts {
		check : self.getDeployments().notEmpty()
		message : "No artifacts are deployed for node: " + self
	}
	
}

context UML!Artifact {

    constraint NameIsSet {
        check : self.name.isDefined()
        message : "Name of Artifact is not defined: " + self
    }

	constraint ArtifactIsDeployed {
		check : UML!Node.all.stream().flatMap(n|n.getDeployments().stream()).filter(d|d.getDeployedArtifacts().contains(self)).findFirst().isPresent()
		message : "There is no Node found that deploys this artifact: " + self
	}

	constraint ArtifactIsManifestedInComponent {
		check : self.getManifestations().stream().filter(m|m.getSuppliers().stream().filter(s|s.isTypeOf(UML!Component)).findFirst().isPresent()).findFirst().isPresent()
		message : "There was no manifestation found that is connected to a component for artifact: " + self + " -- manifestations: " + self.getManifestations()
	}
}

operation UML!NamedElement getMessageStereotypeNotApplied(stereotypeName : String) : String {
	return "The Stereotype '" + stereotypeName + "' is not applied to '" + self + "'"; 
}


